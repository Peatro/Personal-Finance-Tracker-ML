plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'pft'
    version = '0.0.1-SNAPSHOT'
    description = 'Personal Finance Tracker Monorepo'

    repositories {
        mavenCentral()
    }
}

// Включаем плагин dependency-management в subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        // Lombok нужен почти везде
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'

        // Общие тестовые зависимости
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    // Spring Cloud BOM
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
        }
    }
}
